buildscript {
	repositories {
		google()
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:3.1.0'
		classpath 'com.deploygate:gradle:1.1.5'
	}
}
apply plugin: 'deploygate'
apply plugin: 'com.android.application'

File lp = project.rootProject.file('local.properties')
if (lp.exists()) {
	Properties properties = new Properties()
	properties.load(lp.newDataInputStream())
	project.ext.set("sdkDir", properties.getProperty('sdk.dir'))
}
else {
	project.ext.set("sdkDir", System.getenv("ANDROID_HOME"))
}

allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
		}
	}
}

def gitSha() {
	return 'git rev-parse --short HEAD'.execute().text.trim()
}
def gitLastCommit() {
	return 'git log -1 --no-merges --pretty=oneline --abbrev-commit'.execute().text.trim()
}

android {
	compileSdkVersion 28
	buildToolsVersion "28.0.1"

	signingConfigs {
		release {
			storeFile file("postcode.keystore")
			storePassword "android"
			keyAlias "mykey"
			keyPassword "android"
		}
	}
	buildTypes {
		release {
			signingConfig signingConfigs.release
		}
	}

	flavorDimensions "standard"

    productFlavors {
		production {
			dimension "standard"
		}
		dogfood {
			dimension "standard"
			versionName "DF-${gitSha()}"
			buildToolsVersion "28.0.1"
		}
	}

	variantFilter { variant ->
		if (variant.buildType.name.equals('release')
			&& variant.getFlavors().get(0).name.equals('dogfood')) {
				variant.setIgnore(true);
			}
		}
}

deploygate {
	userName = "palfrey"
	token = System.getenv('DEPLOYGATE_KEY')
	apks {
		dogfood {
			sourceFile = file("build/outputs/apk/postcode-dogfood-debug.apk")
		}
	}
}

task(runTest, dependsOn: 'assembleDogfood', type: JavaExec) {
	main = 'net.tevp.postcode.PostcodeBackend'
	classpath = files( 'build/intermediates/classes/dogfood/debug/', 'json.jar', project.sdkDir + '/platforms/android-28/android.jar')
	args '51.5269721','-0.0836098'
}
